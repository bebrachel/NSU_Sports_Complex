plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco'
	id "org.sonarqube" version "5.1.0.4882"
	id 'checkstyle'
}

version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

sonar {
	properties {
		property "sonar.projectKey", "bebrachel_NSU_Sports_Complex"
		property "sonar.organization", "bebrachel"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.java.checkstyle.reportPaths", "build/reports/checkstyle/main.xml"
	}
}

checkstyle {
	toolVersion = '8.45' // or any specific version you need
	configFile = file("./checkstyle/checkstyle.xml")
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

tasks.withType(Checkstyle) {
	reports {
		xml.required = true // Needed for SonarCloud to read Checkstyle reports
		html.required = false
	}
}

dependencies {
	implementation 'org.postgresql:postgresql:42.7.4'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'com.h2database:h2'
	implementation 'org.springframework.boot:spring-boot-starter'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

	if (project.hasProperty('useDocker')) {
		developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	}
}


tasks.named('test') {
	useJUnitPlatform()
}

tasks.jacocoTestReport {
	dependsOn test // Ensure tests run before generating the report

	reports {
		xml.required = true  // Generate an XML report for SonarQube/SonarCloud
		html.required = true // Optional: Generate an HTML report for local inspection
		csv.required = false // Optional: Generate CSV report
	}
}
